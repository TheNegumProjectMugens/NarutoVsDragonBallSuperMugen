;===============================================================================
; INTELIGENCIA ARTIFICIAL:
;===============================================================================
[Statedef -2]

[State 0, Run]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/2
value = ifelse(pos y >= 0,60,65)

[State 0, Run Atras]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = backedgebodydist > 50
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 100 && p2movetype = A
trigger1 = random < (ailevel * 100)/4
value = ifelse(pos y >= 0,70,75)

[State 0, Guard]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = (p2bodydist x < 50 && p2movetype = A) || (enemy,numproj > 0)
trigger1 = random < (ailevel * 100)/2
value = ifelse(statetype = A,132,130)

; GOLPES NORMALES:

[State 0, A,B & C]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = ifelse(random < 500,200,ifelse(random < 500,300,400))

[State 0, Air Combo]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [-30,30]
trigger1 = random < (ailevel * 100)
value = 600

; SPECIALS:

[State 0, Orochizukai no Jutsu]
type = ChangeState
Triggerall = power >= 1000
triggerall = numhelper(1030) = 0
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,150]
trigger1 = random < (ailevel * 100)/2
value = 1000

[State 0, Gogyo Fuin]
type = ChangeState
triggerall = numhelper(4000) = 0
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/2
value = 1100

[State 0, Mandara no Jin]
type = ChangeState
triggerall = numhelper(1250) = 0
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,150]
trigger1 = random < (ailevel * 100)
value = 1200

[State 0, Regeneration]
type = ChangeState
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x >= 100
trigger1 = random < (ailevel * 100)*2
value = 1300

[State 0, Yamata no Jutsu]
type = ChangeState
triggerall = numhelper(1450) = 0
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [150,300]
trigger1 = random < (ailevel * 100)*1.25
value = 1400

[State 0, Kuchiyose Sanju Rashomon]
type = ChangeState
triggerall = numhelper(1550) = 0
Triggerall = power >= 2000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 50
trigger1 = random < (ailevel * 100)
value = 1500

[State 0, Down + B Throw]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x >= 100
trigger1 = random < (ailevel * 100)/4
value = 350
;===============================================================================
;---------------------------Estados usados Siempre------------------------------
;===============================================================================
[Statedef -3]
[State 0, Helper de Conexiones]
type = Helper
Triggerall = numhelper(700) = 0
trigger1 = stateno = 200 && movecontact
Trigger2 = stateno = 210 && helper(215),movecontact
trigger3 = stateno = 221 && time > 10
trigger4 = stateno = 230 && numhelper(235) = 0 && time > 10
trigger5 = stateno = 300 && movecontact
Trigger6 = stateno = 310 && helper(315),movecontact
trigger7 = stateno = 320 && movecontact
trigger8 = stateno = 400 && movecontact
trigger9 = stateno = 410 && movecontact
trigger10 = stateno = 420 && time > 25
trigger11 = stateno = 430 && movecontact
trigger12 = stateno = 1000 && time > 30 && helper(1050),stateno != 1050
trigger13 = stateno = 1101 && time > 40
trigger14 = stateno = 1200 && time > 100
trigger15 = stateno = 1400 && time > 210
trigger16 = stateno = 1500 && time > 120
helpertype = normal
name = "Helper de Conexiones"
ID = 700
stateno = 700
pos = 999999,999999
postype = p1

[State 0, PosSet]
type = PosSet
trigger1 = prevstateno = 200 && time = 1
y = 0
;===============================================================================
; KAGE BUNSHIN:
;-------------------------------------------------------------------------------
[State 0, HitOverride]
type = HitOverride
trigger1 = stateno = [4000,4999]
attr = SCA,NA,SA,HA,NP,SP,HP,NT,ST,HT
stateno = 4001
time = 99999999999999999999999999999999999999999
ignorehitpause = 1
;===============================================================================
; Air Jump y Limitador Air Jump:
;-------------------------------------------------------------------------------
[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 0 && stateno = 40 && prevstateno = 50
trigger2 = var(5) = 0 && stateno = 40 && prevstateno = [60,70]
v = 5
value = 1

[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 1 && pos y >= 0
v = 5
value = 0

[State 0, Air Jump]
type = ChangeState
triggerall = stateno = 50
triggerall = pos y < -20
triggerall = var(5) = 0
triggerall = time > 10
trigger1 = ailevel = 0 && command = "up"
trigger2 = roundstate = 2 && ailevel > 0 && random < (ailevel * 100) && p2bodydist y < - 30
trigger3 = roundstate = 2 && ailevel > 0 && random < (ailevel * 100)/8
value = 40
ctrl = 1
;===============================================================================
; GUARD SPARK:
;-------------------------------------------------------------------------------
[State 0, Guard Spark]
type = Explod
triggerall = numexplod(7010) = 0
trigger1 = moveguarded = 1
anim = 7010
ID = 7010
postype = P2
pos = 8,-30
facing = 1
sprpriority = 2
supermove = 1
removetime = -2
bindtime = 1
ownpal = 1
scale = .6,.6
removeongethit = 1
ontop = 1
ignorehitpause = 1

;===============================================================================
; Pared Rota:
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = numhelper(1050) = 0
trigger1 = p2stateno = 1036
helpertype = normal
name = "Pared Rota"
ID = 1050
stateno = 1050
pos = 0,-30
postype = p2
facing = 1
keyctrl = 0
ownpal = 1

[State 0, Super Jump]
type = PlaySnd
triggerall = stateno = 80
trigger1 = Time = 1
value = S60,1
volume = 55
channel = -1

